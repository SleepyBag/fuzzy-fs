#!/bin/zsh

# for debug
export field=main
autoload -Uz debug_var

# get absolute directory
export base_dir=$(cd $(dirname $0); pwd)
export PATH=$PATH:$base_dir/core
fpath+=($base_dir/core)
export fpath

# determine some basic commands
export preview_command=${fuzzy_fs_preview_command:-$base_dir/module/preview}
export ls_panel_command=$base_dir/module/ls-panel
export history_panel_command=$base_dir/module/history-panel

# params
while getopts "ad-:" param; do
    case "$param" in
        -)
            case "$OPTARG" in
                no-color)
                    enable_color=false
                    ;;
            esac
            ;;
        a)
            show_hidden=true
            ;;
        d)
            debug_mode=true
            ;;
    esac
done
export enable_color=${enable_color:-true}
export show_hidden=${show_hidden:-false}
export debug_mode=${debug_mode:-false}
export EDITOR=${EDITOR:-vim}

export ls_command=${fuzzy_fs_ls_command:-$base_dir/core/_ls}
export history_command=${fuzzy_fs_history_command:-$base_dir/core/_history}
export open_command=${fuzzy_fs_open_command:-"xdg-open"}

# [[ -z "$*" ]] && current_path=$*
current_path=.

# main loop
while [[ $res_command != exit ]] ; do
    current_path=$(abspath $current_path)
    debug_var current_path

    res=$($ls_panel_command $current_path)
    query=$(echo $res | head -n1)
    res_command=$(echo $res | head -n2 | tail -n1)
    target=$(echo $res | tail -n+3)
    debug_var res
    debug_var query
    debug_var res_command
    debug_var target
    case "$res_command" in
        enter)
            if [[ -d $target ]] {
                   current_path=$target
               } else {
                   zsh -c "${=open_command} $target" &> /dev/null
               }
               ;;
        cd)
            current_path=$target
            ;;
        rm)
            rm -ir $target
            ;;
        edit)
            $EDITOR $target
            ;;
        run)
            zsh -c "cd $current_path; $target"
            ;;
        external_command)
            hist_res=$($history_panel_command $current_path)
            debug_var hist_res
            if [[ $(echo $hist_res | head -n2 | tail -n1) != exit ]] {
                   cmd=$(echo $hist_res | tail -n+3 )
                   debug_var cmd
                   export F=$target
                   zsh -c "cd $current_path; $cmd"
               }
               ;;
        exit)
            ;;
        *)
            echo something unexpected happend, please contact the author.
            res_command=exit
            ;;
    esac
done
