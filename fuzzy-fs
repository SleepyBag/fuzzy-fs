#!/bin/zsh

# for debug
local field=main

export base_dir=$(cd $(dirname $0); pwd)
autoload -Uz debug_var
export colors=$(ls $base_dir/colors)
autoload -Uz ${=colors}
autoload -Uz $(ls $base_dir/core)
autoload -Uz $(ls $base_dir/module)

fpath+=($base_dir/core)
fpath+=($base_dir/module)
fpath+=($base_dir/colors)
export fpath

# determine some basic commands
export preview_command=${FUZZY_FS_PREVIEW_COMMAND:-$base_dir/module/preview}
export ls_panel_command=ls-panel
export find_panel_command=find-panel

# params
zparseopts -no-color=no_color \
           a=show_hidden \
           d=debug_mode
export log_file=$base_dir/.log
export EDITOR=${EDITOR:-vim}
export SHELL=${SHELL:-bash}
export TERMINAL=${TERMINAL:-konsole}
[[ -d $HOME/.directory_history && -z "$HISTORY_BASE" ]] && export HISTORY_BASE=$HOME/.directory_history

# tunable commands
export ls_command=${FUZZY_FS_LS_COMMAND:-list-file}
export ls_recent_command=${FUZZY_FS_LS_RECENT_COMMAND:-list-recent-dir}
export find_command=${FUZZY_FS_FIND_COMMAND:-find-recursively}
export history_command=${FUZZY_FS_HISTORY_COMMAND:-list-history}
export ancestors_command=${FUZZY_FS_ANCESTORS_COMMAND:-list-ancestors}
export open_command=${FUZZY_FS_OPEN_COMMAND:-"xdg-open"}

export file_icon=${FUZZY_FS_FILE_ICON:-}
export ancestor_icon=${FUZZY_FS_ANCESTOR_ICON:-}
export directory_icon=${FUZZY_FS_DIRECTORY_ICON:-}
export command_icon=${FUZZY_FS_COMMAND_ICON:-}
export recent_dir_icon=${FUZZY_FS_RECENT_DIR_ICON:-}

function check_empty () {
    var_name="$*"
    if [[ ${(P)var_name} == "" ]] {
           echo "$var_name cannot be empty!"
           exit 1
       }
}
check_empty file_icon
check_empty directory_icon
check_empty command_icon

# [[ -z "$*" ]] && current_path=$*
current_path=.
next_panel_command=$ls_panel_command

# main loop
while [[ $res_command != exit ]] ; do
    current_path=$(abspath $current_path)
    debug_var current_path

    res=$($next_panel_command $current_path $pre_query)
    query=$(echo $res | head -n1)
    res_command=$(echo $res | head -n2 | tail -n1)
    target=$(echo $res | tail -n+3)
    pre_query=""
    debug_var res
    debug_var query
    debug_var res_command
    debug_var target
    case "$res_command" in
        open)
            zsh -c "${=open_command} $target" &> /dev/null
            ;;
        cd)
            current_path=$target
            [[ "$next_panel_command" == "$find_panel_command" ]] && next_panel_command=$ls_panel_command
            ;;
        rm)
            rm -ir $target
            ;;
        edit)
            debug_var current_path
            debug_var target
            edit "$current_path" "$target"
            ;;
        run)
            zsh -c "cd $current_path; $target"
            ;;
        find)
            next_panel_command=$find_panel_command
            ;;
        ls)
            next_panel_command=$ls_panel_command
            ;;
        shell)
            open-shell "$current_path"
            ;;
        toggle_hidden)
            [[ $show_hidden = true ]] && show_hidden=false || show_hidden=true
            pre_query=$query
            ;;
        exit)
            ;;
        *)
            echo something unexpected happend, please contact the author.
            res_command=exit
            ;;
    esac
done
