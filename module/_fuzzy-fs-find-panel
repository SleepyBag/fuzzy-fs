#!/bin/zsh

function _fuzzy-fs-find-panel {
    local field=_fuzzy-fs-find-panel

    # key bindings
    local key_enter=${FUZZY_FS_KEY_ENTER:-Enter}
    local key_toggle_hidden=${FUZZY_FS_KEY_TOGGLE_HIDDEN:-Ctrl-A}
    local key_exit=${FUZZY_FS_KEY_EXIT:-Ctrl-Q}
    local key_abort=${FUZZY_FS_KEY_FIND:-Ctrl-F}
    local key_edit=${FUZZY_FS_KEY_EDIT:-Ctrl-O}
    local key_refresh=${FUZZY_FS_KEY_REFRESH:-Ctrl-R}
    local key_shell=${FUZZY_FS_KEY_SHELL:-Ctrl-I}
    local key_parent=${FUZZY_FS_KEY_PARENT:-Ctrl-U}

    # parameters and settings
    local pwd="$1"
    local init_query="$2"
    [[ -z "$no_color" ]] && local color_param=--ansi

    # get header
    if [[ -z "$no_header" ]]; then
        local header
        header+=$'\n'$(_fuzzy-fs-green "Open Directory:")$'\t'$(_fuzzy-fs-white "$key_enter")
        header+=$'\n'$(_fuzzy-fs-green "Toggle Hidden:")$'\t'$(_fuzzy-fs-white "$key_toggle_hidden")
        header+=$'\n'$(_fuzzy-fs-green "Exit:")$'\t'$(_fuzzy-fs-white "$key_exit")
        header+=$'\n'$(_fuzzy-fs-green "Edit:")$'\t'$(_fuzzy-fs-white "$key_edit")
        header+=$'\n'$(_fuzzy-fs-green "Refresh:")$'\t'$(_fuzzy-fs-white "$key_refresh")
        header+=$'\n'$(_fuzzy-fs-green "Go parent:")$'\t'$(_fuzzy-fs-white "$key_parent")
        header+=$'\n'$(_fuzzy-fs-green "Exit Find Mode:")$'\t'$(_fuzzy-fs-white "$key_abort")
        header+=$'\n'$(_fuzzy-fs-green "Start Shell Here:")$'\t'$(_fuzzy-fs-white "$key_shell")
        header+=$'\n'$'\n'
    else
        header=""
    fi
    header+="Directory: $pwd"
    header+=$'\n'" "

    # show fzf and get result
    local res
    res=$(
        ${=find_command} $pwd | \
            fzf --prompt="Filter: " \
                --no-multi \
                --header="$header" \
                --print-query \
                --query="$init_query" \
                --height=100% \
                $color_param \
                --preview="$preview_command {-1}" \
                --bind "Ctrl-C:cancel" \
                --expect "$key_enter,$key_toggle_hidden,$key_exit,$key_refresh,$key_shell,$key_edit,$key_abort,$key_parent"
       )
    local fzf_status=$?

    # handle fzf return value
    case "$fzf_status" in
        # no match
        1) res+=$'\n' ;;
        # error
        2) return 1 ;;
        # interrupted
        130) res=$'\n'"$key_exit"$'\n'
    esac

    # if fzf returned successfully, return
    local key item
    query=$(echo $res | head -n1)
    key=$(echo $res | head -n2 | tail -n1)
    item=$(echo $res | tail -n+3)

    next_panel=_fuzzy-fs-ls-panel
    case "$key" in
        $key_enter)
            cmd=cd
            if [[ -f $item ]]; then
                target=$(dirname $item)
            else
                target=$item
            fi
            ;;
        $key_toggle_hidden)
            next_panel=_fuzzy-fs-find-panel
            cmd=toggle_hidden
            ;;
        $key_abort)
            cmd=refresh
            ;;
        $key_exit)
            cmd=exit
            ;;
        $key_refresh)
            next_panel=_fuzzy-fs-find-panel
            cmd=refresh
            ;;
        $key_edit)
            cmd=edit
            local filename=$item
            target=$filename
            ;;
        $key_parent)
            next_panel=_fuzzy-fs-find-panel
            cmd=cd
            target=$(dirname $pwd)
            ;;
        $key_shell)
            cmd=shell
            local filename=$item
            if [[ -f $filename ]]; then
                target=$(dirname $filename)
            else
                target=$filename
            fi
            ;;
    esac
}
